<!DOCTYPE aesl-source>
<network>


<!--list of global events-->
<event size="1" name="rx"/>
<event size="1" name="tx"/>


<!--list of constants-->
<constant value="2500" name="PROXHORIZONTALE"/>
<constant value="150" name="MOTOR"/>
<constant value="100" name="TIMER0"/>
<constant value="1000" name="TIMER1"/>
<constant value="0" name="INITIALISATION"/>
<constant value="1" name="BALLADE"/>
<constant value="2" name="LANGUE"/>
<constant value="3" name="EMOTIONS"/>
<constant value="4" name="ACTIVITE"/>
<constant value="5" name="CATCHME"/>
<constant value="6" name="DANCE"/>
<constant value="7" name="POSTBALLADE"/>
<constant value="9" name="PAUSE"/>
<constant value="1" name="TRUE"/>
<constant value="0" name="FALSE"/>
<constant value="1" name="FRANCAIS"/>
<constant value="2" name="ALLEMAND"/>
<constant value="4" name="ANGLAIS"/>
<constant value="500" name="DETECT"/>


<!--show keywords state-->
<keywords flag="true"/>


<!--node thymio-II-->
<node nodeId="1" name="thymio-II"># Thymio II : Dialogue robotique avec le robot Thymio II
# Projet par N.Mojon ETML

# Declaration des constante

# PROXHORIZONTALE = indique la valeur a laquel les capteurs horizontale détacte un obstacle

# MOTOR = Vitesse des roues

# TIMER0 = Vitesse du timer 0

# TIMER1 = Vitesse du timer 1

# INITIALISATION = Etat appellant la methode initialisation et permettant de reinitialisé toute les variables

# BALLADE = Etat appalant la methode de ballade qui permet au robot de se déplacé indépendament en évitant les obstacles

# LANGUE = Etat appellant la methode langue qui permet lors de la communication entre deux robots de choisir la langue commune qui sera la langue de discussion

# EMOTIONS = Etat appellant la methode émotion, qui permet la discussion a proprement parlé des deux robots.
# ACTIVITE = Etat appellant la methode activité permettant d'effectué une des deux activité a Choix

# CATCHME = Etat lancant la première activité possible, celle qui permet a un robots de suivre l'autre.

# DANCE = Etat permettant de lancé la deuxième activitée qui consiste a faire une danse commune avec une musique syncronisée

# POSTBALLADE = methode permettant après avoir fini une activité, de séparer les thymio pour éviter la reconnection instantanée

# TRUE &amp; FALSE = Variable bolléen crée a la main

# FRANCAIS ALLEMAND ANGLAIS = variable pour le choix des Langue

# DETECT = variable de detection de proximité pour les 2 robots lors de l'activité CatchMe

var Son = FALSE # Delai pour la transmission des couleurs.
var Lumiere = FALSE # Variable pour savoir si les lampes sont allumées ou non.
var Identifiant = 2
var Etat = 0
var EtatPause = -1
var LangueRecue = 0
var i = 0
var LangueRecueBinaire[3]
var LangueParleeBinaire[3] = [1,0,1]
var ChoixLangue = 0
var DifferenceVitesse = 0
var VitesseBase=150
var speed_l=0
var max = 0
var mi = 0
var t = 0
var NActivite = 0
var ActiviteDecide = 0
var Reponse = 0
var Initiateur = 0
var DelaiTransmission = 0
var DelaiRep = 0
var DelaiAct = 0
var Parle = FALSE
var PostBallade = 0
var Rencontre = 0
var DemiTour = 0
var TempsCatchMe = 0
var TempsDemiTour = 0
var PremiereParole = 0
var Dance
var NbDance = 0
var NbRencontre = 0
var ReponseAct = 0
call sound.system(-1)
call prox.comm.enable(1)
call leds.top (0,0,0)
call leds.circle (0,0,0,0,0,0,0,0)
timer.period[0] = 0
timer.period[1] = 0


###############################################

sub Initialisation

prox.comm.rx = 0
prox.comm.tx = 0
motor.left.target = 0
motor.right.target = 0
Etat = 0
EtatPause = -1
LangueRecue = 0
i = 0
LangueRecueBinaire = [0,0,0]
ChoixLangue = 0
DifferenceVitesse = 0
VitesseBase=0
speed_l=0
max = 0
mi = 0
t = 0
NActivite = 0
Reponse = 0
Initiateur = 0
DelaiTransmission = 0
DelaiRep = 0
DelaiAct = 0
Parle = FALSE
PostBallade = 0
TempsCatchMe = 0
PremiereParole = 0
Dance = 0
NbDance = 0
DemiTour = FALSE
TempsDemiTour = 0
ActiviteDecide = 0
ReponseAct = 0
call leds.top (0,0,0)
call leds.circle (0,0,0,0,0,0,0,0)
timer.period[1] = 0

################################################

sub Ballade # mode ballade

	prox.comm.tx = Identifiant
	
	if DemiTour == TRUE and TempsDemiTour != 15 then
		motor.right.target = MOTOR
		motor.left.target = -MOTOR
		TempsDemiTour++
		if  TempsDemiTour == 15 then
			DemiTour = FALSE
		end
	end

	while  prox.horizontal[4] > PROXHORIZONTALE 
	or prox.horizontal[3] > PROXHORIZONTALE 
	or prox.horizontal[2] > PROXHORIZONTALE do
		motor.left.target = -MOTOR
		motor.right.target = MOTOR
	end
	
	while  prox.horizontal[0] > PROXHORIZONTALE 
	or prox.horizontal[2] > PROXHORIZONTALE do
		motor.left.target = MOTOR
		motor.right.target = -MOTOR
	end
	 
	if prox.horizontal[2] == 0 
	or prox.horizontal[0] == 0 
	or prox.horizontal[4] == 0 then 
		motor.left.target = MOTOR
		motor.right.target = MOTOR
	end
	
	if  prox.comm.rx != 0  
	and prox.horizontal[2] >= 2000 then
		prox.comm.tx = FRANCAIS + ANGLAIS
		motor.left.target = 0
		motor.right.target = 0
		Etat = 2
	end

################################################

sub BinaireLangue

i = 0

while LangueRecue != 0 do
	LangueRecueBinaire[i] = LangueRecue%2
	LangueRecue = LangueRecue/2
	if i != 2 then
	i++
	end
end

################################################

sub ChoixLangue

i = 0

while LangueParleeBinaire[i] == LangueRecueBinaire[i] do
	ChoixLangue = i
	if i != 2 then
	i++
	end
end

Etat = 3
################################################

sub Langue

LangueRecue = prox.comm.rx

	if  Son == 1 then
		call sound.play(2) #bjr
	end
	
callsub BinaireLangue
callsub ChoixLangue

prox.comm.tx = 0

################################################

sub ChoixActivité

call math.rand(NActivite)

if  NActivite &lt; 0 then
	NActivite = -NActivite
end

NActivite = NActivite%3

################################################

sub RepActivite

call math.rand(Reponse)

if  Reponse &lt; 0 then
	Reponse = -Reponse
end

Reponse = Reponse%3

################################################

sub PostBallade

timer.period[1] = TIMER1

while PostBallade != 5  do
	while  prox.horizontal[4] > PROXHORIZONTALE 
	or prox.horizontal[3] > PROXHORIZONTALE 
	or prox.horizontal[2] > PROXHORIZONTALE do
		motor.left.target = -MOTOR
		motor.right.target = MOTOR
	end
	
	while  prox.horizontal[0] > PROXHORIZONTALE 
	or prox.horizontal[2] > PROXHORIZONTALE do
		motor.left.target = MOTOR
		motor.right.target = -MOTOR
	end
	 
	if prox.horizontal[2] == 0 then 
		motor.left.target = MOTOR
		motor.right.target = MOTOR
	end
	return
end

if  PostBallade == 5 then
	call leds.top(0,0,0)
	Etat = 0
	Rencontre++
	PostBallade = 0
end
	
################################################

sub DelaiParole

call math.rand(PremiereParole)

if  PremiereParole &lt; 0 then
	PremiereParole = -PremiereParole
end

PremiereParole = PremiereParole%51

################################################

sub Emotions

#mettre selection de la 1ère parole
when Parle == FALSE do
	callsub DelaiParole
end

if  Parle == FALSE and PremiereParole == 0 and prox.comm.rx == 0 then
	if  Son == TRUE then
		call sound.play(1) #bjr
	end
	if  Lumiere == TRUE then
		call leds.top(16,16,16) # blanc
	end
	prox.comm.tx = 1
	Parle = TRUE
end

if  DelaiTransmission > 20 then
	
	if  Etat == 3 and prox.comm.rx == 1 then
		if  Son == TRUE then
			call sound.play(1) #bjr
		end
		prox.comm.tx = 2
		if  Lumiere == TRUE then
			call leds.top(32,32,32) # blanc
		end
		Parle = TRUE
	end
	
	if DelaiTransmission > 40 then
	
		if  Etat == 3 
		and prox.comm.rx == 2 
		and ActiviteDecide == FALSE then
			Initiateur = TRUE
			callsub ChoixActivité
#			NActivite = 0 # Valeur pour test
			if NActivite == 1 then
				if  Son == TRUE then
					call sound.play(7) #Catch
				end
				if  Lumiere == TRUE then
					call leds.top(0,0,32) # bleu
				end
				prox.comm.tx = 3
				ActiviteDecide = TRUE
			elseif  NActivite == 0 then
				if  Son == TRUE then
					call sound.play(8) #Dance
				end
				if  Lumiere == TRUE then
					call leds.top(0,0,32) # bleu
				end
				prox.comm.tx = 3
				ActiviteDecide = TRUE
			elseif NActivite == 2 then
				if  Son == TRUE then
					call sound.play(6)
				end
				if  Lumiere == TRUE then
					call leds.top(32,32,0) # jaune
				end	
				DelaiTransmission = 0	
			end
		end
				
		if  DelaiTransmission > 60 then
			if  DelaiRep > 20 then
				if  Etat == 3 
				and prox.comm.rx == 3 
				and ReponseAct == FALSE	then
					callsub RepActivite
#					Reponse = 0	#Valeur pour test
					if Reponse == 0  then
						if  Son == TRUE then
							call sound.play(4) # OK
						end 
						Etat = 4
						prox.comm.tx = 5
						if  Lumiere == TRUE then
							call leds.top(0,32,0)#vert
						end
						ReponseAct = TRUE
					elseif Reponse == 1 then
						if  Son == TRUE then
							call sound.play(5)# N/A
						end 
						prox.comm.tx = 2
						ActiviteDecide = 0
						if  Lumiere == TRUE then
							call leds.top(32,32,0) #jaune
						end
					elseif Reponse == 2 then
						if  Son == TRUE then
							call sound.play(1) #NO
						end
						Etat = 7
						ReponseAct = TRUE
						if  Lumiere == TRUE then
							call leds.top(32,0,0) #rouge
						end
						prox.comm.tx = 6
						if  Son == 1 then
							call sound.play(6) #Aurevoir
						end
					end
				end
			DelaiRep = 0
			end			
			if  prox.comm.rx == 6 and Etat == 3 then
				Etat = 7
				prox.comm.tx = 0
				if  Lumiere == TRUE then
					call leds.top(0,32,32) #cyan
				end
			end
			
			if  prox.comm.rx == 5 and Etat == 3 then
				Etat = 4
				prox.comm.tx = NActivite
				if  Lumiere == TRUE then
					call leds.top(32,0,32) #violet
				end
			end
		end
	end
end

DelaiTransmission++
DelaiRep++
if  PremiereParole > 0 then
	PremiereParole--
end

################################################

sub CatchMe

VitesseBase=MOTOR
prox.comm.tx = 0

max = prox.horizontal[0]
	mi = 0
	speed_l=0
	for i in 0:4 do 
		if prox.horizontal[i]> max then
			max = prox.horizontal[i]
			mi = i
		end
	end
	
	t = 2 - mi
	DifferenceVitesse = t * (VitesseBase / 2)
	if max > 3500 then
		speed_l = (3500 - max) / 2
	end
	if (max > 4000) then
		speed_l = -VitesseBase 
	end
	if (max &lt; 3000) then
		t = 300 - (max - 1000) / 7
		speed_l = t
	end
	
	if (max &lt; 2000) then
		speed_l = VitesseBase 
	end
	
	if(speed_l > VitesseBase) then
		speed_l = VitesseBase
	end
	if(speed_l &lt; -VitesseBase) then
		speed_l = -VitesseBase
	end
	
	if(max &lt; DETECT) then
		motor.left.target = 0
		motor.right.target = 0
	 else 
		motor.right.target = DifferenceVitesse + speed_l
		motor.left.target = speed_l - DifferenceVitesse
	 end
	 
	 TempsCatchMe++
	 
	 if TempsCatchMe == 250 then
	 	Etat = 7
	 end

################################################

sub Dance

call leds.top(0,0,0)
call leds.circle(8,8,8,8,8,8,8,8)
if  Son == 1 then
	call sound.play(11)
end

when Dance &lt; 20 do
	motor.left.target = MOTOR
	motor.right.target = -MOTOR
	if  Lumiere == 1 then
		call leds.top(32,32,0)
	end
end

when Dance > 20 and Dance &lt; 40 do
	motor.left.target = -MOTOR
	motor.right.target = MOTOR
	if  Lumiere == 1 then
		call leds.top(0,32,32)
	end
end
	
if  Dance == 40 then
	Dance = 0
	NbDance++
end

if  NbDance == 10 then
	Etat = 7
end

Dance++
################################################

sub Activite

if Initiateur == 1 and DelaiAct > 50 then
	if  NActivite == 1 then
		DemiTour = TRUE
		callsub Ballade
	else
		Etat = 6
	end
elseif DelaiAct > 50  then
	if  prox.comm.rx == 1 and Etat == 4 then
		Etat = 5			
	else 
		Etat = 6
	end
end

DelaiAct++

################################################

sub Pause #Mise en pause du robot
	
	if  EtatPause == -1 then
		EtatPause = Etat
	Etat = PAUSE
		if  motor.left.target != 0 then
		motor.left.target = 0
		motor.right.target = 0
		end
	else
		Etat = EtatPause
		EtatPause = -1	
	end
################################################

onevent button.forward #activation du mode ballade

callsub Initialisation
timer.period[0] = TIMER0

################################################

onevent button.backward #Reset

callsub Initialisation
timer.period[0] = 0

################################################

onevent button.center #Active le mode pause

if button.center == 1 then
	callsub Pause
end

################################################

onevent button.left

when  button.left == 1 do

	if  Lumiere == TRUE then
		Lumiere = FALSE

	else
		Lumiere = TRUE
	end
end

################################################

onevent button.right


when  button.right == 1 do
	if Son == TRUE then
		Son = FALSE
	else 
		Son = TRUE
		call sound.system(2)
	end
end

################################################

onevent timer0

if  Etat == INITIALISATION then
	callsub Initialisation
	Etat = 1
elseif Etat == BALLADE then
	callsub Ballade
elseif Etat == LANGUE then
	callsub Langue
elseif Etat == EMOTIONS then
	callsub Emotions
elseif Etat == ACTIVITE then
	callsub Activite
elseif  Etat == CATCHME then
	callsub CatchMe
elseif  Etat == DANCE then
	callsub Dance
elseif  Etat == POSTBALLADE then
	callsub PostBallade
end

onevent timer1

PostBallade++</node>


</network>
